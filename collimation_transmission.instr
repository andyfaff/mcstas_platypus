/*******************************************************************************
* Instrument: collimation transmission
*
* %I
* Written by: Andrew Nelson (andrew.nelson@ansto.gov.au)>
* Date: 10/6/2021
* Origin: ANSTO
* %INSTRUMENT_SITE: ANSTO
*
* %Description
* The PLATYPUS TOF reflectometer at ANSTO
*
* Example: <parameters=values>
*
* %Parameters
* double lambda0: [A] mean wavelength
* double dlambda: [A] Half width of uniform wavelength distribution
* string mode: [S] one of {"FOC", "POL"}.
* double foc_exit: [mm] width of focussing guide exit
* double guide_m: m coating of focussing guide sides
* double ss2vg: [mm] height of slit2 gap
* double ss3vg: [mm] height of slit3 gap
* double L23: [mm] slit2 to slit3 distance
* double ss2hg: [mm] height of slit2 gap
* double ss3hg: [mm] height of slit3 gap
* sample_width: [mm] width of sample


*
* %L
* https://www.ansto.gov.au/our-facilities/australian-centre-for-neutron-scattering/neutron-scattering-instruments/platypus
*
* %E
*******************************************************************************/
DEFINE INSTRUMENT PLATYPUS_ANSTO(double lambda0=11.0, dlambda=10.0, string mode="FOC",
double foc_exit=30.0, double guide_m=4.0, double ss2vg=6.955,
double ss3vg=2, double L23=2943.0, double L3S=110.0, double ss2hg=50,  double ss3hg=20, double sample_width=20.0)

DECLARE
%{

int USE_FOC = 0;
int USE_POL = 0;
%}

INITIALIZE
%{


if(strcmp(mode, "FOC") == 0){
	USE_FOC = 1;
} else if (strcmp(mode, "POL") == 0){
	USE_FOC = 0;
	USE_POL = 1;
}

%}

TRACE

REMOVABLE COMPONENT platypus_origin = Progress_bar()
AT (0, 0, 0) RELATIVE ABSOLUTE

// this is where all distances are measured from
// it corresponds to chopper1 location
COMPONENT arm = Arm()
AT (0, 0, 0) RELATIVE PREVIOUS

// The vertical divergence supplied by the guide is approximately
// 2 * 0.1 * 3 * 22 = 13 degrees.
// However the vertical divergence accepted by the slits is much
// smaller than this, thereby reducing the efficiency of the
// calculation. To improve statistics supply a focus_ah that is
//  accepted by the collimation in order to improve the statistics.
// If gravity is turned on then a higher vertical divergence of the
// source is required to fill the vertical divergence, as the calculated
// value will be too stringent. This is the factor of 10 in focus_ah.
// 20 * 180 * (ss2vg + ss3vg) / L23 / 3.142,
REMOVABLE COMPONENT source_div = Source_div(
    xwidth=0.05, 
    yheight=20 / 1000.0, 
    focus_aw=2 * 0.1 * 3 * 22, 
    focus_ah=0.5,
    lambda0=lambda0, 
    dlambda=dlambda)
AT (0, 0, 0) RELATIVE PREVIOUS


COMPONENT slit2 = Slit(
    xwidth=ss2hg / 1000.0, 
    yheight=ss2vg / 1000.0)
AT (0, 0, 0) RELATIVE arm
ROTATED (0, 0, 0) ABSOLUTE 


// centred at 991 mm from slit2
COMPONENT FOC_mirror = Guide_gravity(
    w1=0.05,
    h1=0.1, 
    w2=foc_exit/1000.0, 
    h2=0.1, 
    l=1.2, 
    mleft=guide_m, 
    mright=guide_m, 
    mtop=0,
    mbottom=0)
WHEN(USE_FOC) AT (0, 0, 0.391) RELATIVE slit2
ROTATED(0, 0, 0) RELATIVE ABSOLUTE


COMPONENT pol_mirror = Pol_mirror(
    zwidth=0.05, 
    yheight=0.5)
WHEN(USE_POL) AT (0, 0, 0.641) RELATIVE slit2
ROTATED (89.2, 90, 0) RELATIVE PREVIOUS


COMPONENT slit3 = Slit(
    xwidth=ss3hg / 1000.0, 
    yheight=ss3vg/1000.0)
AT (0, 0, L23/1000.0) RELATIVE slit2
ROTATED (0, 0, 0) ABSOLUTE 


// distance calculated by FARO
COMPONENT sample = Slit(
    xwidth=(sample_width) / 1000.0, 
    yheight=0.05)
AT (0, 0, L3S) RELATIVE slit3
ROTATED (0, 0, 0) ABSOLUTE 


COMPONENT psdlin_monitor = PSDlin_monitor(
    nx=101, 
    filename="xpos", 
    xwidth=0.16,
    yheight=0.2,
    restore_neutron=1)
AT (0, 0, 2.512) RELATIVE sample


COMPONENT l_monitor = L_monitor(
    nL=36, 
    xwidth=0.16,
    yheight=0.2, 
    Lmin=2.25, 
    Lmax=20.25,
    filename="det"
)
AT (0, 0, 2.512) RELATIVE sample

FINALLY
%{
%}

END
